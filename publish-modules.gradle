apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'An Android library for new in-app-purchase api'
                    url = 'https://github.com/extremevn/ebilling'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://github.com/extremevn/ebilling/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'justin-lewis'
                            name = 'Justin Lewis Keith'
                            email = 'lewiskeithjustin@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/extremevn/ebilling.git'
                        developerConnection = 'scm:git:ssh://github.com/extremevn/ebilling.git'
                        url = 'https://github.com/extremevn/ebilling/tree/main'
                    }
                }
            }
            debug(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.debug
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'An Android library for new in-app-purchase api'
                    url = 'https://github.com/extremevn/ebilling'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://github.com/extremevn/ebilling/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'justin-lewis'
                            name = 'Justin Lewis Keith'
                            email = 'lewiskeithjustin@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/extremevn/ebilling.git'
                        developerConnection = 'scm:git:ssh://github.com/extremevn/ebilling.git'
                        url = 'https://github.com/extremevn/ebilling/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
                def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}